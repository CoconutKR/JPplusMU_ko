/*---GFX: Spritesets---*/
//Purchase Sprites
spriteset (spriteset_115_purchase,	                                                    "gfx/JNR/emu115/purchase.png")               {tmpl_purchase_dh(0, 0)}
alternative_sprites(spriteset_115_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,         "gfx/JNR/emu115/purchase_32bpp.png")         {tmpl_purchase_dh_32bpp(0, 0)}

//115 Shonan
spriteset (spriteset_115_front_shonan,    	    	"gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_115_back_shonan,    		      "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_115_mid_shonan,    		      "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_115_back_panto_shonan,    		"gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_115_mid_panto_shonan,    		"gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }

spriteset (spriteset_115_rev_front_shonan,    	    	"gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_115_rev_back_shonan,    		      "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_115_rev_mid_shonan,    		      "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_115_rev_back_panto_shonan,    		"gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_115_rev_mid_panto_shonan,    		"gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }

spriteset (spriteset_115_revflip_front_shonan,    	    	"gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_115_revflip_back_shonan,    		      "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_115_revflip_mid_shonan,    		      "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_115_revflip_back_panto_shonan,    		"gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_115_revflip_mid_panto_shonan,    		"gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }

alternative_sprites(spriteset_115_front_shonan,         ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-1_32bpp.png")        {tmpl_std(0, 1)}
alternative_sprites(spriteset_115_back_shonan,          ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-1_32bpp.png")        {tmpl_std_rev(0, 26)}
alternative_sprites(spriteset_115_mid_shonan,           ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-3_32bpp.png")        {tmpl_std(0, 1)}
alternative_sprites(spriteset_115_back_panto_shonan,    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-2_32bpp.png")        {tmpl_std_rev(0, 26)}
alternative_sprites(spriteset_115_mid_panto_shonan,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-4_32bpp.png")        {tmpl_std(0, 1)}

alternative_sprites(spriteset_115_rev_front_shonan,         ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-1_32bpp.png")        {tmpl_std_rev(0, 26)}
alternative_sprites(spriteset_115_rev_back_shonan,          ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-1_32bpp.png")        {tmpl_std(0, 1)}
alternative_sprites(spriteset_115_rev_mid_shonan,           ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-3_32bpp.png")        {tmpl_std_rev(0, 1)}
alternative_sprites(spriteset_115_rev_back_panto_shonan,    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-2_32bpp.png")        {tmpl_std(0, 1)}
alternative_sprites(spriteset_115_rev_mid_panto_shonan,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-4_32bpp.png")        {tmpl_std_rev(0, 1)}

alternative_sprites(spriteset_115_revflip_front_shonan,         ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-1_32bpp.png")        {tmpl_std(0, 26)}
alternative_sprites(spriteset_115_revflip_back_shonan,          ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-1_32bpp.png")        {tmpl_std_rev(0, 1)}
alternative_sprites(spriteset_115_revflip_mid_shonan,           ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-3_32bpp.png")        {tmpl_std(0, 1)}
alternative_sprites(spriteset_115_revflip_back_panto_shonan,    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-2_32bpp.png")        {tmpl_std_rev(0, 1)}
alternative_sprites(spriteset_115_revflip_mid_panto_shonan,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu115/shonan/115-1-4_32bpp.png")        {tmpl_std(0, 1)}

/*---END OF SECTION---*/



/*---GFX: Pantograph Positions---*/
//115 Shonan
switch(FEAT_TRAINS,SELF, sw_115_gfx_pantograph_pos_shonan_under7, position_in_vehid_chain%4){
  0: spriteset_115_mid_panto_shonan;
  2: spriteset_115_mid_panto_shonan;
  default: spriteset_115_mid_shonan;
}
switch(FEAT_TRAINS,SELF, sw_115_gfx_pantograph_pos_shonan_over7, position_in_vehid_chain%5){
  0: spriteset_115_mid_panto_shonan;
  3: spriteset_115_mid_panto_shonan;
  default: spriteset_115_mid_shonan;
}
switch(FEAT_TRAINS,SELF, sw_115_gfx_pantograph_pos_shonan_8cars, position_in_vehid_chain%7){
  0: spriteset_115_mid_panto_shonan;
  1: spriteset_115_revflip_front_shonan;
  4: spriteset_115_front_shonan;
  5: spriteset_115_mid_panto_shonan;
  default: spriteset_115_mid_shonan;
}
switch(FEAT_TRAINS,SELF, sw_115_gfx_pantograph_pos_shonan_8cars_greencar, position_in_vehid_chain%7){
  0: spriteset_115_mid_panto_shonan;
  1: spriteset_115_revflip_front_shonan;
  //3:165 Series Green Car;
  4: spriteset_115_rev_front_shonan;
  5: spriteset_115_mid_panto_shonan;
  default: spriteset_115_mid_shonan;
}
switch(FEAT_TRAINS,SELF, sw_115_gfx_pantograph_pos_shonan_8cars_refitcheck, cargo_subtype){
  //1: sw_115_gfx_pantograph_pos_shonan_8cars_greencar;
  0: sw_115_gfx_pantograph_pos_shonan_8cars;
}
switch(FEAT_TRAINS,SELF, sw_115_gfx_pantograph_pos_shonan, num_vehs_in_vehid_chain){
  0..4: sw_115_gfx_pantograph_pos_shonan_under7;
  6: sw_115_gfx_pantograph_pos_shonan_8cars_refitcheck;
  default: sw_115_gfx_pantograph_pos_shonan_over7;
}
/*---END OF SECTION---*/



/*---GFX: Train Reversing Switches---*/
//Livery Selection
switch(FEAT_TRAINS,SELF, sw_115_gfx_rev_front_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
//1: ;
0:spriteset_115_rev_front_shonan;
}
switch(FEAT_TRAINS,SELF, sw_115_gfx_revflip_front_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
//1: ;
0:spriteset_115_revflip_front_shonan;
}

switch(FEAT_TRAINS,SELF, sw_115_gfx_rev_mid_panto_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
//1: ;
0:spriteset_115_rev_mid_panto_shonan;
}
switch(FEAT_TRAINS,SELF, sw_115_gfx_revflip_mid_panto_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
//1: ;
0:spriteset_115_revflip_mid_panto_shonan;
}

switch(FEAT_TRAINS,SELF, sw_115_gfx_rev_mid_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
//1: ;
0:spriteset_115_rev_mid_shonan;
}
switch(FEAT_TRAINS,SELF, sw_115_gfx_revflip_mid_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
//1: ;
0:spriteset_115_revflip_mid_shonan;
}

switch(FEAT_TRAINS,SELF, sw_115_gfx_rev_back_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
//1: ;
0:spriteset_115_rev_back_shonan;
}
switch(FEAT_TRAINS,SELF, sw_115_gfx_revflip_back_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
//1: ;
0:spriteset_115_revflip_back_shonan;
}

switch(FEAT_TRAINS,SELF, sw_115_gfx_rev_back_panto_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
//1: ;
0:spriteset_115_rev_back_panto_shonan;
}
switch(FEAT_TRAINS,SELF, sw_115_gfx_revflip_back_panto_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
//1: ;
0:spriteset_115_revflip_back_panto_shonan;
}

//Check for flipped vehicles (dual_headed)
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_front_flippedcheck, vehicle_is_flipped) {
  1: sw_115_gfx_rev_front_livery;
 default: sw_115_gfx_revflip_front_livery;
}
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_mid_panto_flippedcheck, vehicle_is_flipped) {
  1: sw_115_gfx_revflip_mid_panto_livery;
 default: sw_115_gfx_rev_mid_panto_livery;
}
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_mid_flippedcheck, vehicle_is_flipped) {
  1: sw_115_gfx_revflip_mid_livery;
 default: sw_115_gfx_rev_mid_livery;
}
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_back_panto_flippedcheck, vehicle_is_flipped) {
  1: sw_115_gfx_revflip_back_panto_livery;
 default: sw_115_gfx_rev_back_panto_livery;
}
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_back_flippedcheck, vehicle_is_flipped) {
  1: sw_115_gfx_revflip_back_livery;
 default: sw_115_gfx_rev_back_livery;
}

//Pantograph Position Testing
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_pantograph_pos_under7, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x000000FF, 0x41]%4]){ //look at opposite position_in_vehID_chain
  0: sw_115_gfx_rev_mid_panto_flippedcheck;
  2: sw_115_gfx_rev_mid_panto_flippedcheck;
  default: sw_115_gfx_rev_mid_flippedcheck;
}
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_pantograph_pos_over7, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x000000FF, 0x41]%5]){ //look at opposite position_in_vehID_chain
  0: sw_115_gfx_rev_mid_panto_flippedcheck;
  3: sw_115_gfx_rev_mid_panto_flippedcheck;
  default: sw_115_gfx_rev_mid_flippedcheck;
}
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_pantograph_pos_8cars, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x000000FF, 0x41]%7]){ //look at opposite position_in_vehID_chain
  0: sw_115_gfx_rev_mid_panto_flippedcheck;
  1: sw_115_gfx_rev_back_flippedcheck;
  4: sw_115_gfx_rev_front_flippedcheck;
  5: sw_115_gfx_rev_mid_panto_flippedcheck;
  default: sw_115_gfx_rev_mid_flippedcheck;
}
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_pantograph_pos, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 16, 0x000000FF, 0x41]]){ //look at opposite num_vehs_in_vehid_chain
  0..4: sw_115_gfx_rev_pantograph_pos_under7;
  6: sw_115_gfx_rev_pantograph_pos_8cars;
  default: sw_115_gfx_rev_pantograph_pos_over7;
}
switch(FEAT_TRAINS,SELF, sw_115_gfx_rev_mid_refitcheck, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){ //look at opposite refit livery id
//1: ;
0: sw_115_gfx_rev_pantograph_pos;
}

//Back Car Pantograph Testing
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_back_pantocheck, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x000000FF, 0x41]]){ //look at opposite position_in_vehID_chain
 0: sw_115_gfx_rev_back_flippedcheck;
 default: sw_115_gfx_rev_back_panto_flippedcheck;
}

//Front/Back Car Position Testing
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_frontback_nomucars, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist-1, 0x10F), var[0x61, 0, 0x000000FF, 0x41]%2]){ //look at opposite position_in_vehID_chain
  1:sw_115_gfx_rev_front_flippedcheck;
  0:sw_115_gfx_rev_back_pantocheck;
}
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_frontback_aftermucars, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist-1, 0x10F), var[0x61, 0, 0x000000FF, 0x41]%2]){ //look at opposite position_in_vehID_chain
  1:sw_115_gfx_rev_back_pantocheck;
  0:sw_115_gfx_rev_front_flippedcheck;
}
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_frontback_mucarcheck, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F),STORE_TEMP(var[0x61, 0, 0x000000FF, 0x41],0),  //look at opposite position_in_vehID_chain
STORE_TEMP((position_in_consist_from_end-position_in_consist-1)-LOAD_TEMP(0), 0x10F),                               //look at what is ahead of the first vehicle in the vehID_chain
var[0x61, 0, 0x0000FFFF, 0xC6]]){
  1000:sw_115_gfx_rev_frontback_aftermucars;
  default:sw_115_gfx_rev_frontback_nomucars;
}
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_frontback_sameid, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist+1, 0x10F), var[0x61, 0, 0x0000FFFF, 0xC6]]){ //check ID of vehicle behind of opposite vehicle
  2025:sw_115_gfx_rev_frontback_mucarcheck;
  1000:sw_115_gfx_rev_front_flippedcheck;
  default:sw_115_gfx_rev_back_pantocheck;
}
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_frontback, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist-1, 0x10F), var[0x61, 0, 0x0000FFFF, 0xC6]]){ //check ID of vehicle infront of opposite vehicle
  2025:sw_115_gfx_rev_frontback_sameid;
  1000:sw_115_gfx_rev_back_pantocheck;
  default:sw_115_gfx_rev_front_flippedcheck;
}

//ID Testing
switch(FEAT_TRAINS, SELF, sw_115_gfx_rev_idcheck, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xC6]]){ //check ID of opposite vehicle
  1000: sw_115_gfx_rev_pantograph_pos;
  2025: sw_115_gfx_rev_frontback;
}
/*---END OF SECTION---*/



/*---GFX: Front/Back Cab Car Switches + DUAL_HEADED fix---*/
//115 Shonan
switch(FEAT_TRAINS, SELF, sw_115_gfx_back_pantocheck_shonan, position_in_vehid_chain){
 0: spriteset_115_back_shonan;
 default: spriteset_115_back_panto_shonan;
}
switch(FEAT_TRAINS, SELF, sw_115_gfx_frontback_flipcheck_shonan, vehicle_is_flipped){
 0: spriteset_115_front_shonan;
 default: sw_115_gfx_back_pantocheck_shonan;
}
switch(FEAT_TRAINS, PARENT, sw_115_gfx_frontback_revcheck_shonan, vehicle_is_reversed){
 0: sw_115_gfx_frontback_flipcheck_shonan;
 default: sw_115_gfx_rev_idcheck;
}
/*---END OF SECTION---*/



/*---GFX: Middle Car Switches---*/
//115 Shonan
switch(FEAT_TRAINS, PARENT, sw_115_gfx_mucar_revcheck_mid_shonan, vehicle_is_reversed){
    0: sw_115_gfx_pantograph_pos_shonan;
    default: sw_115_gfx_rev_idcheck;
}
/*---END OF SECTION---*/



/*---GFX: Livery Refitting Switches---*/
//Main Switches
switch(FEAT_TRAINS, PARENT, sw_115_gfx_mucar,  cargo_subtype) {
  //1: ;
  0: sw_115_gfx_mucar_revcheck_mid_shonan;
}
switch(FEAT_TRAINS, PARENT, sw_115_gfx_main,  cargo_subtype) {
  //1: ;
  0: sw_115_gfx_frontback_revcheck_shonan;
}

//Menu Switches
switch(FEAT_TRAINS, SELF, sw_115_subtypetext_mucar, cargo_subtype) {
  //1: string(); <--with 165 green car in 8-car formation
  0: string(SHONAN_LV);
}
switch(FEAT_TRAINS, SELF, sw_115_subtypetext_main, cargo_subtype) {
  //1: string(); <--with 165 green car in 8-car formation
  0: string(SHONAN_LV);
}
/*---END OF SECTION---*/



//Coupling
  switch(FEAT_TRAINS, SELF, sw_115_attach_vehid, [vehicle_type_id == emu_115 || vehicle_type_id == mu_car]) {
  0: return string(MU_UNIQUE);
  }



item(FEAT_TRAINS, emu_115) {
    property {
        name:                         string(JNR_115);
        introduction_date:            date(1963, 03, 01);
        model_life:                   20;
        vehicle_life:                 39;
        reliability_decay:            11;

        cargo_capacity:               param_capacity_local;
        loading_speed:                param_loading_3D;
        cost_factor:                  17;
        running_cost_factor:          51;

        speed:                        110 km/h;
        track_type:                   NARROW_OHLE;
        power:                        220 kW;
        weight:                       37 ton;

        climates_available:           ALL_CLIMATES;
        ai_special_flag:              AI_FLAG_PASSENGER;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        misc_flags:                   bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes:     bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost:                   0;
        dual_headed:                  1;
        ai_engine_rank:               0;

        running_cost_base:            RUNNING_COST_ELECTRIC;
        engine_class:                 ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 4, ENABLE_WAGON_POWER);

        tractive_effort_coefficient:  0.2;
        air_drag_coefficient:         0.07;
        bitmask_vehicle_info:         0;
        length: 8;
    }
    /*Graphics and Callbacks*/
      graphics {
        default:                      sw_115_gfx_main;
		    purchase:                 spriteset_115_purchase;
		    additional_text:	      return string(JNR_115_DESC);
        cargo_subtype_text:           sw_115_subtypetext_main;
        can_attach_wagon:			  sw_115_attach_vehid;
        //start_stop:                   ;
    }
    /*Multiple Unit Car*/
      livery_override(mu_car){
        default:                      sw_115_gfx_mucar;
        cargo_capacity:               param_capacity_local;
        loading_speed:                param_loading_3D;
        cargo_subtype_text:           sw_115_subtypetext_mucar;
        power: 110;
        weight: 27;
      }

}